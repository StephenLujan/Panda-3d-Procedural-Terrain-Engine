//Cg
//
//Cg profile arbvp1 arbfp1

void vshader(float4 vtx_position : POSITION,
             float2 vtx_texcoord0 : TEXCOORD0,
             out float4 l_position : POSITION,
             out float2 l_texcoord0 : TEXCOORD0,
             uniform float4x4 mat_modelproj)
{
  l_position=mul(mat_modelproj, vtx_position);
  l_texcoord0=vtx_texcoord0;
}


void fshader(float2 l_texcoord0 : TEXCOORD0,
             uniform float4 k_casterpos,
             uniform float4 k_vlparams,
             uniform float4 k_vlcolor,
             uniform sampler2D k_src : TEXUNIT0,
             out float4 o_color : COLOR)
{
  float decay = 1.0f;
  float2 curcoord = l_texcoord0;
  float2 lightdir = curcoord - k_casterpos.xy;
  lightdir *= k_vlparams.y;
  half3 sample = tex2D(k_src, curcoord);
  float3 vlcolor = sample;
  for (int i = 0; i < 32; i++) {
    curcoord -= lightdir;
    sample = tex2D(k_src, curcoord);
    sample *= decay;
    vlcolor += sample;
    decay *= k_vlparams.z;
  }
  o_color.rgb = min(vlcolor * k_vlcolor.www * k_vlcolor.rgb, k_vlparams.www) * 2.0;
  o_color.a = 0.0f;
}

